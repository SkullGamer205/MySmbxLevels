//DragonCoins

OnEvent,0,0,1,1002,0,DragonCoin1
OnEvent,0,0,1,1003,0,DragonCoin2
OnEvent,0,0,1,1004,0,DragonCoin3
OnEvent,0,0,1,1005,0,DragonCoin4
OnEvent,0,0,1,1006,0,DragonCoin5
OnEvent,0,0,0,2012,0,Timer - Start                     // Start timer (#1010)
OnEvent,0,0,0,2011,0,Timer - Pause                     // Pause Timer (#1011)
OnEvent,0,0,0,2016,0,Timer - Stop                      // Stop Timer  (#1016)
OnEvent,0,0,0,2017,0,Timer - Points                    // Transform time to points
OnEvent,0,0,0,1020,0,Star1 - Collected
OnEvent,0,0,0,1021,0,Star2 - Collected
OnEvent,0,0,0,1022,0,Star3 - Collected

#-1

//Draw Pictures in Screen (CUSTOM HUD)

LoadImage,1,0xDC00FF,0,0,0,HUD.png
LoadImage,2,0xDC00FF,0,0,0,Mario.png
LoadImage,3,0xDC00FF,0,0,0,Luigi.png
LoadImage,4,0xDC00FF,0,0,0,Peach.png
LoadImage,5,0xDC00FF,0,0,0,Toad.png
LoadImage,6,0xDC00FF,0,0,0,Link.png
LoadImage,7,0xDC00FF,0,0,0,health.png
LoadImage,8,0xDC00FF,0,0,0,health-line.png
LoadImage,9,0xDC00FF,0,0,0,dragon-coin.png
LoadImage,10,0xDC00FF,0,0,0,star.png
LoadImage,11,0xDC00FF,0,0,0,Interface0.png             // key
LoadImage,12,0xDC00FF,0,0,0,Interface8.png             // bomb
LoadImage,13,0xDC00FF,0,0,0,Interface6.png             // rupee

//Place Pictures (CUSTOM HUD)
PlaceSprite,1,1,0,0

//Pull out the values of coins, lives, points from the game. (CUSTOM HUD)

$CurrentCoins,SetVar,1,0,0,-1,1,0
$CurrentLifes,SetVar,1,0,0,-1,2,0
$CurrentPoint,SetVar,1,0,0,-1,4,0
$CurrentStars,SetVar,1,0,0,-1,3,0
$CurrentBombs,SetVar,1,0,0,-1,8,0
$Timer,SetVar,1,0,400,-1,5,0
$TimerState,SetVar,1,0,0,-1,6,0

#0
LunaControl,2,1,0,0,0,0
FilterReservePowerup,0,0,0,0,0,0            // Block ReverseBox
IfVar,0,0,100,2019,0,Timer                  // Warns the player about the elapsed time (#2019)
//Assign values to coins, lives and points. (CUSTOM HUD)

OnPlayerMem,0xF0,3,2,1013,w
OnPlayerMem,0x16,0,1,1014,w                 // If >0 hearts (1) then Active #1001
OnPlayerMem,0x16,1,1,1015,w                 // If >1 hearts (2) then Active #1002
OnPlayerMem,0x16,2,1,1016,w                 // If >2 hearts (3) then Active #1003
OnPlayerMem,0x12,0,1,1017,w

$CurrentCoins,LoadGlobalVar,0x00B2C5A8,0,0,0,0,w
$CurrentLifes,LoadGlobalVar,0x00B2C5AC,0,0,0,0,f
$CurrentPoint,LoadGlobalVar,0x00B2C8E4,0,0,0,0,w
$CurrentStars,LoadGlobalVar,0x00B251E0,0,0,0,0,w
$CurrentBombs,LoadPlayerVar,0,0x08,0,0,0,w

//Show (or paste) Custom variables (for HUD)
$CurrentCoins,ShowVar,0,156,12,1,0,
$CurrentLifes,ShowVar,0,80,12,1,0,
$CurrentPoint,ShowVar,0,210,12,1,0,
$CurrentStars,ShowVar,0,80,34,1,0,
Timer,0,2010,0,0,1,0

//Display "PLAYER_NAME" (For HUD)
OnPlayerMem,0xF0,1,0,1008,w
OnPlayerMem,0xF0,2,0,1009,w
OnPlayerMem,0xF0,3,0,1010,w
OnPlayerMem,0xF0,4,0,1011,w
OnPlayerMem,0xF0,5,0,1012,w

$Timer,ShowVar,0,156,34,1,0

//Dragon Coins in HUD
#1002
PlaceSprite,1,9,264,34,0,0
#1003
PlaceSprite,1,9,278,34,0,0
#1004
PlaceSprite,1,9,292,34,0,0
#1005
PlaceSprite,1,9,306,34,0,0
#1006
PlaceSprite,1,9,320,34,0,0s

#1008                       // "PLAYER_NAME"
PlaceSprite,1,2,32,14,2,0
#1009
PlaceSprite,1,3,32,14,2,0
#1010
PlaceSprite,1,4,32,14,2,0
#1011
PlaceSprite,1,5,32,14,2,0
#1012
PlaceSprite,1,6,32,14,2,0
PlaceSprite,1,12,228,34,2,0
$CurrentBombs,ShowVar,0,246,34,1,1,        // Draw Bombs
PlaceSprite,1,13,124,12,2,0                // Draw Rupee

//Heart System (For HUD)
#1013
PlaceSprite,1,0,4,6,3,0                    // Draw Line instead Hearts
#1014
PlaceSprite,1,7,6,40,3,0                   // Draw 1 Heart
#1015
PlaceSprite,1,7,6,24,3,0                   // Draw 2 Hearts
#1016
PlaceSprite,1,7,6,8,3,0                    // Draw 3 Hearts
#1017
PlaceSprite,1,11,210,32,2,0                // Draw Key
#END

// Draw Stars
#1020
PlaceSprite,1,10,643,14,0,0
#1021
PlaceSprite,1,10,699,14,0,0
#1022
PlaceSprite,1,10,755,14,0,0

//Custom Timer
#2010
Timer,0,2010,0,0,44,1
IfVar,0,0,1,2013,1,TimerState
#2011
$TimerState,SetVar,1,0,0,-1,1,0          // Pause Timer
#2012
$TimerState,SetVar,1,0,1,-1,1,0          // Play-Continue Timer
#2013                                    // (Uses a built-in timer that triggers #2011) (Run every time the call is)
Timer,0,2014,0,0,1,1
IfVar,0,2,1,2015,1,Timer                 //
#2014                                    // (With each passed command, subtract the value of the timer by 1 and send a signal to #1010)
$Timer,SetVar,1,2,1,-1,1,0
#2015
Kill,0,0,0,0,1,0
Trigger,0,2016,0,0,1,0
$Timer,SetVar,1,0,0,-1,1,0
#2016
$TimerState,SetVar,1,0,0,-1,1,0
DeleteEventsFrom,2013,0,0,0,1,0
DeleteEventsFrom,2014,0,0,0,1,0
DeleteEventsFrom,2015,0,0,0,1,0         // Stops Timer
#2017                                   // Timer - Points
IfVar,0,1,0,2018,1,Timer                // If $Timer > 0, then gave points and play sound
IfVar,0,2,1,2020,1,Timer                // If $Timer < 1, then play another sound
#2018
$Timer,SetVar,1,2,1,-1,1,0
MemAssign,0x00B2C8E4,100,1,0,1,w
PlaySFX,0,0,0,128,1,score-tally.wav
Trigger,0,2017,0,0,0,0
#2019
PlaySFX,0,0,0,128,1,hurry-up.wav
DeleteEventsFrom,2019,0,0,0,1,0
#2020
PlaySFX,0,0,0,128,1,score-tally-end.wav
DeleteEventsFrom,2020,0,0,0,1,0
#END
